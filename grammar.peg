{
	var INSTRUCTION_INDEX = 1;
    var VARIABLE_INDEX = 3;
}

startrule
 = program
  
program
= (instructionSet /
    comments)+
    
comments = c:((whitespace*)? "/*" (.[^*]*) "*/" (whitespace*)?) {
	return {type: "comment"};
}

instructionSet =
	stringInstruction / 
	variableInstruction / 
	variableVariableAssignmentInstruction / 
	variableLiteralAssignmentInstruction /
    variableJsonLiteralAssignmentInstruction /
    showTableInstruction
    
stringInstruction
= i:((whitespace*)? instruction space+ stringParam (whitespace*)?) { 
   var line = i;

    var tokParam = "";
    var tokInstr = line[INSTRUCTION_INDEX];
    var type= "stringInstruction";

    line[3][1].map(function(e){
      e.map(function(ec){
      tokParam += ec.join("");
      });
    });

    return {type, tokInstr,tokParam};
 }
 
variableInstruction
= i:((whitespace*)? instruction space+ ID (whitespace*)?) { 
   var line = i;

    var tokParam = line[VARIABLE_INDEX].join("");
    var tokInstr = line[INSTRUCTION_INDEX];
    var type= "variableInstruction";
    
    return {type, tokInstr,tokParam};
 }

word = letter+

letter = [a-zA-Z0-9]

whitespace = " "
/ "\r"
/ "\n"
/ "\t"

space = " "

newline = "\n"

showTableInstruction 
 = i:((whitespace*)? "show table with data" (whitespace)+ ID (whitespace*)?) {
 	var line = i;
    var tokInstr = "showTable";
    var tokDataParam = line[3].join("");
    var type = "showTableInstruction";
    
 	return {type, tokInstr, tokDataParam};
 }

variableVariableAssignmentInstruction = i:((whitespace*)?ID (whitespace)* "=" (whitespace)* ID (whitespace*)?) {
	var line = i;

    var tokLHS = line[1].join("");
    var tokRHS = line[5].join("");
    var type= "variableVariableAssignmentInstruction";
    
    return {type, tokLHS,tokRHS};
}

variableLiteralAssignmentInstruction = i:((whitespace*)? ID (whitespace)* "=" (whitespace)* stringParam (whitespace*)?) {
	var line = i;
	var tokRHS = "";
    var tokLHS = line[1].join("");
    var type= "variableLiteralAssignmentInstruction";
    
    line[5][1].map(function(e){
      e.map(function(ec){
      tokRHS += ec.join("");
      });
    });

    return {type, tokLHS,tokRHS};
}

variableJsonLiteralAssignmentInstruction = i:((whitespace*)? ID (whitespace)* "=" (whitespace)* jsonParam (whitespace*)?) {
	var line = i;
	var tokRHS = line[5];
    var tokLHS = line[1].join("");
    var type= "variableJsonLiteralAssignmentInstruction";
    return {type, tokLHS,tokRHS};
}

instruction 
= log
 /alert

ID = word

stringParam = p:('"' (word (space*)?)* '"') { return p;}

jsonParam = JSON_text

log = "log"
alert = "alert"

// the JSON grammar part is taken from the pegjs repo under examples: https://github.com/pegjs/pegjs/blob/master/examples/json.pegjs

// JSON Grammar
// ============
//
// Based on the grammar from RFC 7159 [1].
//
// Note that JSON is also specified in ECMA-262 [2], ECMA-404 [3], and on the
// JSON website [4] (somewhat informally). The RFC seems the most authoritative
// source, which is confirmed e.g. by [5].
//
// [1] http://tools.ietf.org/html/rfc7159
// [2] http://www.ecma-international.org/publications/standards/Ecma-262.htm
// [3] http://www.ecma-international.org/publications/standards/Ecma-404.htm
// [4] http://json.org/
// [5] https://www.tbray.org/ongoing/When/201x/2014/03/05/RFC7159-JSON

// ----- 2. JSON Grammar -----

JSON_text
  = ws value:value ws { return value; }

begin_array     = ws "[" ws
begin_object    = ws "{" ws
end_array       = ws "]" ws
end_object      = ws "}" ws
name_separator  = ws ":" ws
value_separator = ws "," ws

ws "whitespace" = [ \t\n\r]*

// ----- 3. Values -----

value
  = false
  / null
  / true
  / object
  / array
  / number
  / string

false = "false" { return false; }
null  = "null"  { return null;  }
true  = "true"  { return true;  }

// ----- 4. Objects -----

object
  = begin_object
    members:(
      head:member
      tail:(value_separator m:member { return m; })*
      {
        var result = {};

        [head].concat(tail).forEach(function(element) {
          result[element.name] = element.value;
        });

        return result;
      }
    )?
    end_object
    { return members !== null ? members: {}; }

member
  = name:string name_separator value:value {
      return { name: name, value: value };
    }

// ----- 5. Arrays -----

array
  = begin_array
    values:(
      head:value
      tail:(value_separator v:value { return v; })*
      { return [head].concat(tail); }
    )?
    end_array
    { return values !== null ? values : []; }

// ----- 6. Numbers -----

number "number"
  = minus? int frac? exp? { return parseFloat(text()); }

decimal_point
  = "."

digit1_9
  = [1-9]

e
  = [eE]

exp
  = e (minus / plus)? DIGIT+

frac
  = decimal_point DIGIT+

int
  = zero / (digit1_9 DIGIT*)

minus
  = "-"

plus
  = "+"

zero
  = "0"

// ----- 7. Strings -----

string "string"
  = quotation_mark chars:char* quotation_mark { return chars.join(""); }

char
  = unescaped
  / escape
    sequence:(
        '"'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "u" digits:$(HEXDIG HEXDIG HEXDIG HEXDIG) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return sequence; }

escape
  = "\\"

quotation_mark
  = '"'

unescaped
  = [^\0-\x1F\x22\x5C]

// ----- Core ABNF Rules -----

// See RFC 4234, Appendix B (http://tools.ietf.org/html/rfc4234).
DIGIT  = [0-9]
HEXDIG = [0-9a-f]i